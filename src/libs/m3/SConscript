import os

Import('env')

sources = [
    env.Glob('*.cc'), env.Glob('*/*.cc'),
    env.Glob('arch/$ARCHTYPE/*.S'), env.Glob('arch/$ARCHTYPE/*.cc'),
    env.Glob('arch/$ARCH/*.S'), env.Glob('arch/$ARCH/*.cc')
]
if env['ARCH'] != 'host':
    sources += [env.Glob('arch/baremetal/*.cc')]

lib = env.StaticLibrary(
    target = 'libm3',
    source = sources
)
env.Install(env['LIBDIR'], lib)

if env['ARCH'] == 't2':
    cmenv = env.Clone()
    cmenv.Append(CPPFLAGS = ' -DCM=1')

    objs = [cmenv.Object(os.path.splitext(src.abspath)[0] + '-cm.o', src) for src in Flatten(sources)]
    cmlib = cmenv.StaticLibrary(
        target = 'libm3-cm',
        source = objs
    )
    cmenv.Install(cmenv['LIBDIR'], cmlib)
